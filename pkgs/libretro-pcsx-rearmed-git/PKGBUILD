# Maintainer: Cecile Tonglet <cecile.tonglet@gmail.com>
# Contributor: Tomasz Pa≈õ <kierek93@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-pcsx-rearmed-git
pkgver=1786.bde5ee9
pkgrel=1
pkgdesc="libretro implementation of PCSX ReARMed. (PlayStation)"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/libretro/pcsx_rearmed"
license=('GPL')
conflicts=('libretro-pcsx_rearmed-git')
depends=('zlib' 'libretro-core-info' 'mesa')
makedepends=('git')
groups=('libretro')

_libname=pcsx_rearmed_libretro
_gitname=pcsx_rearmed
source=("git+https://github.com/libretro/${_gitname}.git")

md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${_gitname}"
  sed -i "s/R3000Acpu/extern R3000Acpu/" libpcsxcore/r3000a.c
  sed -i "s/psxRegisters/extern psxRegisters/" libpcsxcore/r3000a.c
  sed -i "s/Rcnt rcnts/extern Rcnt rcnts/" libpcsxcore/psxcounters.c
}

build() {
  cd "${_gitname}"
  make platform=rpi4 -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/${_libname}.so" "${pkgdir}/usr/lib/libretro/${_libname}.so"
}
